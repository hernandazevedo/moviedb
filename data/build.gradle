apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'gradle-secretkey'
apply plugin: 'kotlin-kapt'
HashMap<String, String> baseDependencies = rootProject.ext.dependencies
HashMap<String, String> testDependencies = rootProject.ext.testDependencies
HashMap<String, String> application = rootProject.ext.application
HashMap<String, String> versions = rootProject.ext.versions

apply from: '../static-analysis/lint.gradle'
apply from: '../static-analysis/findbugs.gradle'
apply from: '../static-analysis/pmd.gradle'
apply from: '../static-analysis/checkstyle.gradle'
apply from: '../static-analysis/ktlint.gradle'
apply from: '../static-analysis/detekt.gradle'
apply from: "../coverage.gradle"

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    publishNonDefault true
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode application.versionCode
        versionName application.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }

        // Write out the current schema of Room
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':domain')

    implementation baseDependencies.kotlin_std_lib
    implementation baseDependencies.rx_java
    implementation baseDependencies.timber
    api baseDependencies.retrofit
    implementation baseDependencies.retrofitRxAdapter
    implementation baseDependencies.retrofitScalarsConverter
    implementation baseDependencies.okHttpInterceptor
    implementation baseDependencies.gson

    implementation baseDependencies.room
    kapt           baseDependencies.room_compiler
    implementation baseDependencies.room_rxjava2

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
    testImplementation testDependencies.mockitoKotlin

    androidTestImplementation testDependencies.atsl_runner
    androidTestImplementation testDependencies.atsl_rules
    androidTestImplementation testDependencies.room_testing
    androidTestImplementation testDependencies.core_testing
    androidTestImplementation testDependencies.junit
}

secretkey {
    def localProp = new Properties()
    def fileName = 'local.properties'
    if (project.rootProject.file(fileName).exists()) {
        localProp.load(new FileInputStream(rootProject.file(fileName)))
    }
    def omdbApiKey = System.env.OMBD_API_KEY != null ? System.env.OMBD_API_KEY : localProp["omdbApiKey"] ?: ""
    templateFile = "${project.projectDir}/src/main/cpp/include/native-lib-config.h.template"
    keyMap = [OMBD_API_KEY_REPLACE: omdbApiKey]
}

project.tasks.preBuild.dependsOn project.tasks.processKeys
check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'ktlint', 'detekt'